WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT    = _{ "--" ~ (!"\n" ~ ANY)* ~ "\n" }
program    = _{ SOI ~ ((assignment | application | term) ~ ";"?)* ~ EOI }
assignment =  { variable ~ "=" ~ (application | term) }

// Lambda calculus
term             = _{ abstraction | "(" ~ application ~ ")" | untyped_variable | "(" ~ term ~ ")" }
abstraction      =  { ("\\" | "Î»") ~ variable ~ "." ~ term }
application      =  { term ~ term+ }
variable         =  { typed_variable | untyped_variable }
untyped_variable = @{ ASCII_ALPHANUMERIC+ ~ "'"* }
typed_variable   = _{ untyped_variable ~ ":" ~ type_expression }

// Type annotations
type_expression = _{ app_type | base_type }
app_type        =  { base_type ~ "->" ~ type_expression }
base_type       = @{ ASCII_ALPHA+ }
